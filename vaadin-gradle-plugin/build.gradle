/***********************************************************************************************************************
 *
 * Plugins
 *
 **********************************************************************************************************************/
plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'idea'
    id 'com.gradle.plugin-publish' version '0.11.0'
    id 'org.jetbrains.kotlin.jvm' version '2.1.21'
    id 'org.jetbrains.dokka' version '1.8.20'
}

/***********************************************************************************************************************
 *
 * Basic properties
 *
 **********************************************************************************************************************/

def pom = new XmlSlurper().parse(rootProject.file('pom.xml'))
defaultTasks 'jar'
group pom.parent.groupId
version = project.hasProperty('vaadin.version') ? project.getProperty('vaadin.version') : pom.parent.version
archivesBaseName = pom.artifactId
sourceCompatibility = 21
targetCompatibility = 21

/***********************************************************************************************************************
 *
 * Sources
 *
 **********************************************************************************************************************/

sourceSets {
    functionalTest {
       compileClasspath += sourceSets.main.output
       runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    functionalTestImplementation.extendsFrom testImplementation
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

/***********************************************************************************************************************
 *
 * Repositories & Dependencies
 *
 **********************************************************************************************************************/

repositories {
    mavenLocal()
    maven { url 'target/dependencies' }
    maven { url = 'https://maven.vaadin.com/vaadin-prereleases' }
    mavenCentral()
    maven { url = 'https://plugins.gradle.org/m2/' }
}

dependencies {
    implementation('org.jetbrains.kotlin:kotlin-stdlib:2.1.21')
    implementation("com.vaadin:hilla-engine-core:${pom.properties.getAt('hilla.version')}")
    implementation("com.vaadin:flow-gradle-plugin:${pom.properties.getAt('flow.version')}")
    implementation("com.vaadin:flow-plugin-base:${pom.properties.getAt('flow.version')}")
    implementation("com.vaadin:vaadin-prod-bundle:${project.version}")
    implementation("com.vaadin:vaadin-dev-bundle:${project.version}")
    testImplementation("junit:junit:4.13.2")
    testImplementation("org.jetbrains.kotlin:kotlin-test:2.1.21")
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

/***********************************************************************************************************************
 *
 * Artifacts
 *
 **********************************************************************************************************************/

jar {
    manifest{
        attributes(
                'Vaadin-Package-Version': 1,
                'Implementation-Title': 'Vaadin Gradle Plugin',
                'Implementation-Version': archiveVersion,
                'Implementation-Vendor': 'Vaadin Ltd',
        )
    }
}

/**
 * Plugin artifact sources.
 */
task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
    include 'com/**/*.groovy'
    include 'com/**/*.java'
    include 'com/**/*.kt'
}

/**
 * Artifacts in build
 */
artifacts {
    archives jar
    archives sourcesJar
}

/***********************************************************************************************************************
 *
 * Testing & Quality
 *
 ***********************************************************************************************************************/
task functionalTest(type: Test) {
    dependsOn test, jar
    group = 'Verification'

    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    maxParallelForks = 1
    jvmArgs('-Xms512M', '-Xmx1024M')
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}
check.dependsOn functionalTest
/***********************************************************************************************************************
 *
 * Deployment
 *
 ***********************************************************************************************************************/
gradlePlugin {
    testSourceSets sourceSets.functionalTest
    plugins {
        vaadinPlugin {
            id = 'com.vaadin'
            implementationClass = 'com.vaadin.gradle.plugin.VaadinPlugin'
        }
    }
}

pluginBundle {
    website = 'https://vaadin.com/docs/latest/guide/start/gradle'
    vcsUrl = 'https://github.com/vaadin/flow'
    description = 'Build Vaadin applications with Gradle. As of Vaadin 20+, the gradle plugin is released as part of the Vaadin Platform with the same version. ' +
            'For older versions, please follow the link below to learn which Plugin version to use with particular Vaadin version. Vaadin recommends using the latest Vaadin LTS version.'
    tags = ['vaadin']
    plugins {
        vaadinPlugin {
            id = gradlePlugin.plugins.vaadinPlugin.id
            displayName = 'Vaadin Gradle Plugin'
        }
    }
    mavenCoordinates {
        groupId = project.group
        artifactId = project.archivesBaseName
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version

            from components.java
        }
    }
}

/***********************************************************************************************************************
 *
 * Misc.
 *
 **********************************************************************************************************************/
wrapper {
    gradleVersion = '8.14'
    distributionType = Wrapper.DistributionType.ALL
}

// Travis kills the build if there's no stdout for 10 minutes: https://docs.travis-ci.com/user/common-build-problems/#build-times-out-because-no-output-was-received
// This will print ongoing tests, taken from https://stackoverflow.com/a/4292739/377320
functionalTest {
    testLogging {
        info.events "passed", "skipped", "failed"
    }
}

kotlin {
    explicitApi()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "21"
    }
}
