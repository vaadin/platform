buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven { setUrl("https://maven.vaadin.com/vaadin-prereleases") }
    }
    dependencies {
        classpath group: 'com.vaadin',
                name: 'vaadin-gradle-plugin',
                version: "$pluginVersion"
    }
}

plugins {
    id 'war'
    id 'org.gretty' version '4.1.10'
    id 'com.vaadin.plugin.sauce.SauceConnectPlugin' version '0.0.15'
}

apply plugin: 'com.vaadin'
apply plugin: 'com.vaadin.plugin.sauce.SauceConnectPlugin'

repositories {
    mavenCentral()
    mavenLocal()
    maven { setUrl('https://maven.vaadin.com/vaadin-prereleases') }
}

dependencies {
    // Vaadin
    implementation enforcedPlatform("com.vaadin:vaadin-bom:$vaadinVersion")
    implementation 'com.vaadin:vaadin'

    testImplementation 'com.vaadin:vaadin-testbench'
    testImplementation 'junit:junit:4.13'

    providedCompile 'jakarta.servlet:jakarta.servlet-api:6.1.0'

    // logging through the SLF4J API to SLF4J-Simple. See src/main/resources/simplelogger.properties for logger configuration
    implementation 'org.slf4j:slf4j-simple:2.0.3'
}

defaultTasks('clean', 'build')

test {
    filter {
        excludeTestsMatching '*IT'
        setFailOnNoMatchingTests(false)
    }
}

gretty {
    contextPath = '/'
    httpPort = 8080
    servletContainer = 'jetty11'
    integrationTestTask = 'integrationTest'
}

def sauceUser = System.getProperty('sauce.user')
def sauceKey = System.getProperty('sauce.sauceAccessKey')
def sauceIdentifier = '--tunnel-identifier ' + System.currentTimeMillis()
sauceconnect {
    username = sauceUser
    key = sauceKey
    options = '-P 4445 ' + sauceIdentifier;
}

task integrationTest(type: Test) {
    description = 'Runs Integration Tests.'
    group = 'verification'

    if (sauceUser != null) {
        integrationTest.dependsOn startSauceConnect
        integrationTest.finalizedBy stopSauceConnect

        systemProperties 'testDriver' : 'sauce'
        systemProperties 'sauce.user' : sauceUser
        systemProperties 'sauce.sauceAccessKey' : sauceKey
        systemProperties 'sauce.options' : sauceIdentifier
    }

    systemProperties 'com.vaadin.testbench.Parameters.testsInParallel' : System.getProperty('com.vaadin.testbench.Parameters.testsInParallel')
    systemProperties 'test.use.hub' : System.getProperty('test.use.hub')
    if (System.getProperty('vaadin.testbench.developer.license') != null) {
        systemProperties 'vaadin.testbench.developer.license' : System.getProperty('vaadin.testbench.developer.license')
    }
    if (System.getProperty('com.vaadin.testbench.Parameters.hubHostname') != null) {
        systemProperties 'com.vaadin.testbench.Parameters.hubHostname' : System.getProperty('com.vaadin.testbench.Parameters.hubHostname')
    }

    useJUnit()

    maxHeapSize = '1G'
    filter {
        includeTestsMatching('*IT*')
    }

    mustRunAfter test

    testLogging {
        showStandardStreams = true
    }
}

wrapper {
    gradleVersion = '8.14'
    distributionType = Wrapper.DistributionType.ALL
}

check.dependsOn integrationTest
